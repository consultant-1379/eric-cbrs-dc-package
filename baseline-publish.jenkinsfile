#!/usr/bin/env groovy

def defaultBobImage = 'armdocker.rnd.ericsson.se/proj-adp-cicd-drop/bob.2.0:latest'
def bob = new BobCommand()
        .bobImage(defaultBobImage)
        .envVars([
                ARM_API_TOKEN  : '${ARM_API_TOKEN}',
                HOME           : '${HOME}',
                CHART_NAME     : '${CHART_NAME}',
                CHART_REPO     : '${CHART_REPO}',
                CHART_VERSION  : '${CHART_VERSION}',
                GERRIT_REFSPEC : '${GERRIT_REFSPEC}',
                RELEASE        : '${RELEASE}',
                // KUBECONFIG:'${KUBECONFIG}',
                USER           : '${USER}',
                GERRIT_USERNAME: '${GERRIT_USERNAME}',
                GERRIT_PASSWORD: '${GERRIT_PASSWORD}',
                SELI_USER      : '${SELI_USER}',
                SELI_PASSWORD  : '${SELI_PASSWORD}',
                SERO_USER      : '${SERO_USER}',
                SERO_PASSWORD  : '${SERO_PASSWORD}',
                JOB_NAME       : '${JOB_NAME}',
                JENKINS_URL    : '${JENKINS_URL}',
                BUILD_NUMBER   : '${BUILD_NUMBER}',
                SPRINT_TAG     : '${SPRINT_TAG}',
                PRODUCTION_DATE: '${PRODUCTION_DATE}'
        ])
        .needDockerSocket(true)
        .toString()

pipeline {
    agent {
        node {
            label 'GE7_Docker'
        }
    }
    options {
        disableConcurrentBuilds()
    }
    parameters {
        string(name: 'CHART_NAME', defaultValue: 'eric-cbrs-dc', description: 'Chart name e.g.: eric-ms-b')
        string(name: 'CHART_REPO', defaultValue: 'https://arm.seli.gic.ericsson.se/artifactory/proj-eric-cbrs-dc-drop-helm', description: 'Chart repo e.g.: https://arm.seli.gic.ericsson.se/artifactory/proj-emzenyi-helm')
        string(name: 'CHART_VERSION', defaultValue: '1.1.0-28', description: 'Chart version e.g.: 1.0.0-1')
        string(name: 'GERRIT_REFSPEC', defaultValue: ' ', description: 'Gerrit Refspect e.g.: refs/changes/87/4641487/1')
    }
    stages {
        stage('Init') {
            steps {
                sh 'echo Init'
            }
        }
        stage('Getting sprint tag') {
            steps {
                script {
                    env.SPRINT_TAG = sh(script: 'wget -q -O - --no-check-certificate https://ci-portal.seli.wh.rnd.internal.ericsson.com//api/product/ENM/latestdrop/|cut -d\':\' -f2|sed \'s/[},"]//g\'', returnStdout: true).trim()
                }
            }
        }
        stage('Publish Helm Chart') {
            steps {
                withCredentials([
                        usernamePassword(credentialsId: 'Gerrit HTTP', usernameVariable: 'GERRIT_USERNAME', passwordVariable: 'GERRIT_PASSWORD'),
                        usernamePassword(credentialsId: 'SELI_Artifactory', usernameVariable: 'SELI_USER', passwordVariable: 'SELI_PASSWORD'),
                        usernamePassword(credentialsId: 'SERO_Artifactory', usernameVariable: 'SERO_USER', passwordVariable: 'SERO_PASSWORD'),
                        string(credentialsId: 'CBRSCIADM', variable: 'ARM_API_TOKEN')]) {
                    sh 'echo Publish Helm chart'
                    sh "${bob} publish"
                }
            }
        }
        stage('Archive artifact.properties') {
            steps {
                // Archive artifact.properties so Spinnaker can read the parameters
                archiveArtifacts allowEmptyArchive: true, artifacts: 'artifact.properties'
            }
        }
    }
    post {
        always {
            deleteDir()
        }
    }
}

// More about @Builder: http://mrhaki.blogspot.com/2014/05/groovy-goodness-use-builder-ast.html
import groovy.transform.builder.Builder
import groovy.transform.builder.SimpleStrategy

@Builder(builderStrategy = SimpleStrategy, prefix = '')
class BobCommand {

    def bobImage = 'bob.2.0:latest'
    def envVars = [:]

    def needDockerSocket = false

    String toString() {
        def env = envVars
                .collect({ entry -> "-e ${entry.key}=\"${entry.value}\"" })
                .join(' ')

        def cmd = """\
            |docker run
            |--init
            |--rm
            |--workdir \${PWD}
            |--user \$(id -u):\$(id -g)
            |-v \${PWD}:\${PWD}
            |-v /etc/group:/etc/group:ro
            |-v /etc/passwd:/etc/passwd:ro
            |-v \${HOME}:\${HOME}
            |${needDockerSocket ? '-v /var/run/docker.sock:/var/run/docker.sock' : ''}
            |${env}
            |\$(for group in \$(id -G); do printf ' --group-add %s' "\$group"; done)
            |--group-add \$(stat -c '%g' /var/run/docker.sock)
            |${bobImage}
            |"""
        return cmd
                .stripMargin()           // remove indentation
                .replace('\n', ' ')      // join lines
                .replaceAll(/[ ]+/, ' ') // replace multiple spaces by one
    }
}
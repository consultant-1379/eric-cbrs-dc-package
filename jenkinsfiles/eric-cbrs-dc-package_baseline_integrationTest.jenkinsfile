#!/usr/bin/env groovy

def defaultBobImage = 'armdocker.rnd.ericsson.se/proj-adp-cicd-drop/bob.2.0:latest'
def bob = new BobCommand()
        .bobImage(defaultBobImage)
        .envVars([
                HOME                : '${HOME}',
                CHART_NAME          : '${CHART_NAME}',
                CHART_REPO          : '${CHART_REPO}',
                CHART_VERSION       : '${CHART_VERSION}',
                PWD                 : '${PWD}',
                HELM_REPO_TOKEN     : '${HELM_REPO_TOKEN}',
                KUBECONFIG          : '${KUBECONFIG}',
                USER                : '${USER}',
                SELI_USER           : '${SELI_USER}',
                SELI_PASSWORD       : '${SELI_PASSWORD}',
                SERO_USER           : '${SERO_USER}',
                SERO_PASSWORD       : '${SERO_PASSWORD}',
                RELEASE             : '${RELEASE}',
                GERRIT_USERNAME     : '${GERRIT_USERNAME}',
                GERRIT_PASSWORD     : '${GERRIT_PASSWORD}',
                JENKINS_URL         : '${JENKINS_URL}',
                GERRIT_REFSPEC      : '${GERRIT_REFSPEC}',
                ARM_API_TOKEN       : '${ARM_API_TOKEN}',
                ENVIRONMENT_NAME    : 'ieatenmc15a017',
                NAMESPACE           : '${NAMESPACE}',
                ENM_ENROLL          : 'true',
                REDUCED_FOOTPRINT   : 'true',
                INT_CHART_VERSION   : '${INT_CHART_VERSION}',
                CSAR_VERSION        : '${CSAR_VERSION}',
                TESTWARE_VERSION    : '${TESTWARE_VERSION}',
                TEST_SPEC_TAG       : '${TEST_SPEC_TAG}',
                INTEGRATION_VALUES  : '${INTEGRATION_VALUES}',
                CERTM_CONFIG_CONTENT: '${CERTM_CONFIG_CONTENT}'
        ])
        .needDockerSocket(true)
        .toString()
pipeline {
    agent {
        node {
            label 'GE7_Docker'
        }
    }
    options {
        lock(params.ENVIRONMENT_NAME + "-" + params.NAMESPACE + "-deployment-resource") // Prevent parallel builds against the same namespace on the same deployment ID
        //lock(label: 'kaas-va', variable: 'RESOURCE_NAME', quantity: 1)
    }
    environment {
        SELI = credentials('SELI_Artifactory')
        SERO = credentials('SERO_Artifactory')
        SERO_USR = "${SERO_USR}"
        GERRIT = credentials('Gerrit HTTP')

        //KUBECONFIG = "${WORKSPACE}/.kube/config"
    }
    stages {
        stage('Credentials') {
            //environment {
            //    K8S_CONFIG_FILE_ID = sh(script: "echo \${RESOURCE_NAME} | cut -d'_' -f1", returnStdout: true).trim()
            //}
            steps {
                //configFileProvider([configFile(fileId: "${K8S_CONFIG_FILE_ID}", targetLocation: "${env.KUBECONFIG}")]) {}
                withCredentials([file(credentialsId: 'cbrsciadm_docker_config', variable: 'dockerConfig')]) {
                    sh "cp ${dockerConfig} ${HOME}/.docker/config.json"
                }
            }
        }
        stage('Init') {
            steps {
                withCredentials([
                        usernamePassword(credentialsId: 'Gerrit HTTP', usernameVariable: 'GERRIT_USERNAME', passwordVariable: 'GERRIT_PASSWORD'),
                        usernamePassword(credentialsId: 'SELI_Artifactory', usernameVariable: 'SELI_USER', passwordVariable: 'SELI_PASSWORD'),
                        usernamePassword(credentialsId: 'SERO_Artifactory', usernameVariable: 'SERO_USER', passwordVariable: 'SERO_PASSWORD'),
                        string(credentialsId: 'CBRSCIADM', variable: 'ARM_API_TOKEN')]) {
                    sh "${bob} init"
                }
            }
        }
        stage('Install') {
            steps {
                sh "${bob} install"
            }
        }
        stage('Test') {
            steps {
                withCredentials([usernamePassword(credentialsId: 'SERO_Artifactory', usernameVariable: 'SERO_USER', passwordVariable: 'SERO_PASSWORD')]) {
                    sh "${bob} integration"
                }
            }
        }
        stage('Uninstall') {
            steps {
                sh "${bob} uninstall"
            }
        }
    }
    post {
        always {
            withCredentials([usernamePassword(credentialsId: 'SERO_Artifactory', usernameVariable: 'SERO_USER', passwordVariable: 'SERO_PASSWORD')]) {
                sh "${bob} report || true"
            }
            archiveArtifacts allowEmptyArchive: true, artifacts: 'k8s-test/target/reports/**/*.*'
            archiveArtifacts allowEmptyArchive: true, artifacts: 'k8s-test/target/logs/**/*.*'
            publishHTML target: [allowMissing: true, alwaysLinkToLastBuild: true, keepAll: true, reportDir: 'k8s-test/target/reports/html-report/', reportFiles: 'index.html', reportName: 'HTML Report', reportTitles: 'HTML Report']
            sh "${bob} clean || true"
        }
    }
}
// More about @Builder: http://mrhaki.blogspot.com/2014/05/groovy-goodness-use-builder-ast.html
import groovy.transform.builder.Builder
import groovy.transform.builder.SimpleStrategy

@Builder(builderStrategy = SimpleStrategy, prefix = '')
class BobCommand {

    def bobImage = 'bob.2.0:latest'
    def envVars = [:]

    def needDockerSocket = false

    String toString() {
        def env = envVars
                .collect({ entry -> "-e ${entry.key}=\"${entry.value}\"" })
                .join(' ')

        def cmd = """\
            |docker run
            |--init
            |--rm
            |--workdir \${PWD}
            |--user \$(id -u):\$(id -g)
            |-v \${PWD}:\${PWD}
            |-v /etc/group:/etc/group:ro
            |-v /etc/passwd:/etc/passwd:ro
            |-v \${HOME}:\${HOME}
            |${needDockerSocket ? '-v /var/run/docker.sock:/var/run/docker.sock' : ''}
            |${env}
            |\$(for group in \$(id -G); do printf ' --group-add %s' "\$group"; done)
            |--group-add \$(stat -c '%g' /var/run/docker.sock)
            |${bobImage}
            |"""
        return cmd
                .stripMargin()           // remove indentation
                .replace('\n', ' ')      // join lines
                .replaceAll(/[ ]+/, ' ') // replace multiple spaces by one
    }
}
